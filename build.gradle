/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
apply plugin: 'java'
apply plugin: 'maven-publish'

group = project.group
version = project.version

task fatJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': 'org.ballerinalang.command.Main',
                'Implementation-Title': 'Ballerina Update Tool',
                'Implementation-Version': version
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

sourceCompatibility = 1.8

dependencies {
    implementation 'info.picocli:picocli:4.0.1'
    implementation 'me.tongfei:progressbar:0.7.4'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version = project.version
            artifact fatJar
        }
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name + "-distribution"
            version = project.version
            artifact  source: createZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/ballerina-update-tool")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


CopySpec copyJarSpec = copySpec {
    from ('build/libs')
    into('/lib')
}

task createZip(type:Zip) {
    dependsOn jar
    with copyJarSpec
    from ('resources')
    destinationDir file('build')
}

build {
    dependsOn createZip
}

repositories {
    mavenLocal()
    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
    }

    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }

    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/ballerina-update-tool'
    }
}
